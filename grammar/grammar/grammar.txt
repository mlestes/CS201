unary : INTEGER
      | REAL
      | VARIABLE
      | VARIABLE OPEN_PAREN expressionlist CLOSE_PAREN
      | prefix unary
      | VARIABLE suffix
      | OPEN_PAREN expression CLOSE_PAREN
      | STRING
      | RETURN expression
      | BREAK
      | CONTINUE
      | PRINT OPEN_PAREN printstatement CLOSE_PAREN

expression : unary
           | unary operator expression
           | unary comparator expression
           | unary logic expression

expressionlist : expression
               | expression expressionlist
               | EMPTY

printstatement : STRING
               | VARIABLE
               | STRING printstatement
               | VARIABLE printstatement

operator : PLUS
         | TIMES
         | MINUS
         | DIVIDE
         | MOD
         | POW
         | ASSIGN
         | QUESTION
         | COLON
         | DOT

comparator: EQUALS_EQUALS
          | GREATER_THAN
          | LESS_THAN
          | GREATER_THAN_EQUALS
          | LESS_THAN_EQUALS
          | NOT comparator

logic : AND
      | OR
      | XOR

prefix : PLUS
       | MINUS
       | PLUS_PLUS
       | MINUS_MINUS
       | ADDRESS
       | STAR

suffix : PLUS_PLUS
       | MINUS_MINUS

ifstate : IF OPEN_PAREN expression CLOSE_PAREN block elsestate

elsestate : ELSE block
          | EMPTY

whilestate : WHILE OPEN_PAREN expression CLOSE_PAREN block

block : BEGIN statementlist END
      | EMPTY

statementlist : statement
              | statement statementlist
              | EMPTY

statement : expression SEMI_COLON
          | vardef
          | funcdef 
          | ifstate
          | whilestate

vardef : VAR VARIABLE ASSIGN expression SEMI_COLON
       | VAR VARIABLE

funcdef : DEFINE VARIABLE OPEN_PAREN parameterlist CLOSE_PAREN block
        | DEFINE VARIABLE SEMI_COLON

parameterlist : VARIABLE
              | VARIABLE parameterlist
              | EMPTY

structdef : STRUCT VARIABLE block

program : funcdef
        | funcdef program
        | structdef
        | structdef program
        | vardef
        | vardef program
